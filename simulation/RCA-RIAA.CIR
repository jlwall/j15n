Phono preamplifier with passive RIAA equalization (RCA).
*
* This Preamplifier corresponds to the circuit 29-16, page
* 702 of the RCA Receiving Tube Manual RC-30.
* A precise RIAA network (not rounded to E12 values) is
* provided as reference. Its output is V(ref).
*
* To wiew the results with Probe:
*  1) Plot a trace for "VDB(out)",
*  2) Plot the reference "63+VDB(ref)".
* Compare the two curves.
*
***********************************************************
* Load model
.INC 12ax7a.inc
*
***********************************************************
* Phono preamplifier circuit
X1    1  in   2   12ax7a
X2    7   4   6   12ax7a
R1   in   0       47k
C0   in   0      150p
R2    2   0      2.7k
R3    8   1      100k
R4    8  Bb       39k
R5   Bb   7      100k
R6    3   4      470k
R7    6   0      2.7k
R8    4   0      680k
R10   4   5       22k
C1    2   0       22u
C2    8   0       22u
C3    1   3      100u
C4    6   0       22u
C6    4   5      3.3n
C7    5   0       10n
C9    7 out      220n
* Load resistor
Rl  out   0      220k
* Supply voltage
VB   Bb   0      250
*
***********************************************************
* Reference RIAA circuit
Rr1  in ref       93.03k
Rr2 ref  99        7.50k
Cr3 ref  99       10.83n
Cr4  99   0       31.57n
*
***********************************************************
* Input signal
Vin  in    0    AC  1
*
***********************************************************
* Literals
.OP
.AC DEC 10 10 100k
* Requires a noise model for the 12AX7A.
*.NOISE V(out) Vin 1
.WIDTH OUT=80
.OPTIONS LIMPTS=10000
.PROBE
.END
